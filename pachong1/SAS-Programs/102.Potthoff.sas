options pageno=min nodate formdlim='-';
title 'Kevin Jenkins'' Potthoff Analysis'; run;
proc format; value I 0='NonIdealist' 1='Idealist';
data kevin; infile 'C:\Users\Vati\Documents\_XYZZY\_Stats\StatData\potthoff.dat'; input ar misanth idealism;
format idealism I. ;       MxI = misanth * idealism;
proc corr; var ar--idealism; run;
title; run;
proc ttest; class idealism; var ar misanth; run;
proc reg; CGI: model ar = misanth idealism MxI;
 C: model ar=misanth;
 CG: model ar = misanth idealism;
 CI: model ar = misanth MxI; RUN; QUIT;
*Easier way to get the test of coincidence -- using the TEST statement;
proc reg; model ar = misanth idealism MxI;
 TEST idealism=0, MxI=0; run; QUIT;
proc sort; by idealism; run;
proc reg simple corr; model ar=misanth; by idealism; RUN; QUIT;
proc sgplot; scatter x = misanth y = ar; reg  x = misanth y = ar;
 yaxis label='Attitude to Animals' grid values=(1 to 5 by 1);
 xaxis label='Misanthropy' grid values=(1 to 5 by 1);
 by idealism; run;
proc sgplot; reg x = misanth y = ar / group = idealism nomarkers;
  yaxis label='Attitude to Animals';
  xaxis label='Misanthropy'; run;
proc sgplot; reg x = misanth y = ar / group = idealism;
  yaxis label='Animal Rights';
  xaxis label='Misanthropy'; run;
Data Centered; set kevin;
MisanthLow = misanth - 1.65; InteractLow = MisanthLow * Idealism;
MisanthMean = misanth - 2.32; InteractMean = MisanthMean * Idealism;
MisanthHigh = misanth - 2.99; InteractHigh = MisanthHigh * Idealism;
Misanth7 = misanth - 7;  Interact7 = Misanth7 * Idealism;
proc reg;
Low: model ar = MisanthLow idealism InteractLow;
Mean: model ar = MisanthMean idealism InteractMean;
High: model ar = MisanthHigh idealism InteractHigh;
WayOut:  model ar = Misanth7 idealism Interact7;
run; Quit;

