******************************************************************************;
*Screen.sas -- Screening a Data Set;
******************************************************************************;
options pageno=min nodate formdlim='-';
data delmel; infile 'C:\Users\Vati\Documents\StatData\Screen.dat'; 
* 1: ALWAYS include an identification number;
input ID 1-3 @5 (Q1-Q138) (1.);
label Q1='Sex' Q3 = 'Age' Q5='CitySize'  Q6='CityCountry' Q67='SibsMental';
******************************************************************************;
* 2:  See Cody & Smith page 330 for use of ARRAYs to recode many variables;
if Q62 = 5 then Q62 = . ; if Q65 = 5 then Q65 = . ; if Q67 = 5 then Q67 = . ;
if Q15 = 5 then Q15 = . ; if Q16 = 5 then q16 = . ;
******************************************************************************;
* 3:  Transform age variable to reduce positive skewness;
age_sr = sqrt(Q3); age_log = log10(Q3); age_inv = -1/(Q3);
******************************************************************************;
* 4:  Dichotomize age variable;
if q3 = 1 then age_di = 1; else if q3 > 1 then age_di = 2;
******************************************************************************;
* 5:  Create siblings variable;
SIBS = Q15 + Q16;
******************************************************************************;
* 6:  Transform sibs variable to reduce positive skewness;
sibs_sr = sqrt(sibs); sibs_log = log10(sibs); sibs_in = -1/sibs;
******************************************************************************;
* 7:  Create mental variable and dummy variable for missing data on Mental;
MENTAL = Q62 + Q65 + Q67;
MentalMiss = 0; If Mental = . then MentalMiss = 1;
******************************************************************************;
* 8:  Transform and reflect/transform Mental to reduce negative skewness;
Mental2 = Mental*Mental;
Mental3 = Mental**3;
Ment_exp = EXP(Mental);
R_Ment = 13 - Mental; R_Ment_sr = sqrt(R_Ment); R_Ment_log = log10(R_Ment);
******************************************************************************;
* 9:  Dichotomize Mental.  BEWARE: SAS codes missing values with a negative number
      -- if you said 'If Mental LE 9 then Ment_di = 1' then observations with
      missing values would be recoded to 1;
if 0 LE Mental LE 9 then Ment_di=1; else if Mental > 9 then Ment_di=2;
******************************************************************************;
* 10:  Check for out of range values and missing data;
title 'Check for out of range values and missing data'; run;
proc means min max n nmiss; var q1-q10 q50-q70; run;

******************************************************************************;
* 11:  Check for skewness etc.  Note use of -- list;
title 'Check for skewness and kurtosis on original and transformed data'; run;
proc means min max n nmiss skewness kurtosis; var Q3 age_sr -- Mental Mental2 -- R_Ment_log; run;
******************************************************************************;
* 12:  Look at distributions in tables; run;
title 'Frequency Distribution Tables';
proc freq; tables q3 age_di sibs mental ment_di; run;
******************************************************************************;
* 13:  Print out bad data, including ID number; run;
title 'Identify the cases with bad data'; run;
data duh; set delmel; if q9 > 3; proc print; var q9; id id; run;
******************************************************************************;
* 14:  Check pattern of missing data on the Mental variable;
title 'Can we predict Mental missing data from other variables?'; run;
proc corr nosimple data=delmel; var MentalMiss; with Q1 Q3 Q5 Q6 sibs; run;
******************************************************************************;
* 15:  Find multivariate outliers and compare them with the overall centroid;
title 'Identifying and describing multivariate outliers'; run;
proc reg data=delmel; model id = Q1 Q3 Q6 mental; output out=hat H=Leverage; run;
data outliers; set hat; if leverage > .052;
title 'The multivariate outliers'; run;
proc print; var id Q1 Q3 Q6 mental leverage; run;
proc means mean; var Q1 Q3 Q6 mental; run;
title 'The overall means';
proc means mean stddev data=delmel; var Q1 Q3 Q6 mental; run;
