options formdlim='-';
title 'Approximate Randomization Test, Equality of Means, 2 Independent Samples'
; run;
*Chen & Dunlap, Behavior Research Methods, Instruments, & Computers, 1993;
*25(3) 406-409;
data orisam; infile 'D:StatData\art-indt.data'; input X Y;
*Put name of your data file in the infile statement;
*X is group membership, numerically coded, Y is continuous dependent variable;
proc summary; var y; class X; output out=actstic mean=ymean;
data actstic (keep=original); set actstic; original=abs(dif(ymean)); if x=2;
data shuffle (keep=sample x y); nop=1000;
array temp(*) s1-s800;
*if total sample size N > 800, replace 800 with N;
do sample = 1 to nop; do i=1 to obn; temp(i)=i; end;
do j=1 to obn - 1; k=int(ranuni(0)*(obn - j + 1)) + j; index = temp(k);
  temp(k) = temp(j); temp(j) = index; set orisam(keep=y) point=index;
  set orisam(keep=x) point=j nobs=obn; output; end; end; stop;
proc summary; by sample; var y; class x; output out=pseudo mean=smean;
data pseudo (keep=sig); if _n_ = 1 then set actstic; set pseudo;
  pseustic=abs(dif(smean)); if x=2; if pseustic ge original then sig=1;
  else sig=0;
proc summary; var sig; output out=final sum=nos;
data final; set actstic; set final; set pseudo nobs=nop; sig=nos/nop;
a=nop - nos + 1; nos = nos + 1; pro5=probbeta(.05, nos, a);
pro1=probbeta(.01, nos, a); file print;
put @ 10 'Original Statistic = 'original;
put @ 10 'Estimated Significance Level p (two-tailed) = 'sig;
put @ 10 'Prob(phi <= 0.05) = 'pro5;
put @ 10 'Prob(phi <= 0.01) = 'pro1; run;
