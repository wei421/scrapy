options pageno=min nodate formdlim='-';
TITLE 'Multivariate approach to Mixed ANOVA with 2 Within-Subjects';
title2 'and one Between-Subjects Factors: Howell p. 486, 8th ed.'; run;
proc format; value gr 1='A-B' 2='A-A' 3='L-A-B';
data sup; infile 'C:\Users\Vati\Documents\StatData\MAN_2W1B.dat';
INPUT group c1p1 c1p2 c2p1 c2p2 c3p1 c3p2 c4p1 c4p2;
format group gr.;
****************************************************************************;
proc anova; class group;
model c1p1 -- c4p2 = group / nouni; repeated cycle 4, phase 2 / printe;
title3 'Omnibus Analysis';
****************************************************************************;
proc anova; class group;
model c1p1 c2p1 c3p1 c4p1 = group / nouni; repeated cycle 4;
title 'Simple effects at Phase 1 -- Shock';
****************************************************************************;
proc anova; class group; model c1p2 c2p2 c3p2 c4p2 = group; repeated cycle 4;
means group / lsd lines;
title 'Simple effects at Phase 2 -- No Shock -- and simple simple main';
title2 'effects of group at each level of Cycle for Phase 2';
title3 'with pairwise comparisons among Groups.'; run;
****************************************************************************;
proc sort; by group;
proc anova; model c1p2 c2p2 c3p2 c4p2 =  / nouni; repeated cycle 4; by group;
title 'Simple, simple main effect of Cycle for each Group during Phase 2'; run;
